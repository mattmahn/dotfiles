#!/bin/zsh

# Enable Powerlevel10k instant prompt. Should stay close to the top of ~/.zshrc.
# Initialization code that may require console input (password prompts, [y/n]
# confirmations, etc.) must go above this block, everything else may go below.
# if [[ -r "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh" ]]; then
#   source "${XDG_CACHE_HOME:-$HOME/.cache}/p10k-instant-prompt-${(%):-%n}.zsh"
# fi

# something with performance tracing
#zmodload zsh/zprof

# shortcut to this dotfiles path is $DOTFILES
export DOTFILES="$HOME/.dotfiles"

# your project folder that we can `c [tab]` to
export PROJECTS="$HOME/code"

# make sure the system knows how I like to edit
export EDITOR=nvim
export VISUAL=nvim
export GIT_EDITOR=$EDITOR
export PAGER=less

# all of our zsh files
typeset -U config_files
config_files=($DOTFILES/*/*.zsh)

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

                                                             ### ZSH Plugins {{{
# Set the name of the static .zsh plugins file antidote will generate.
zsh_plugins=${HOME}/.zsh_plugins.zsh
# Ensure you have a .zsh_plugins.txt file where you can add plugins.
[[ -f ${zsh_plugins:r}.txt ]] || touch ${zsh_plugins:r}.txt
# Lazy-load antidote.
fpath+=(${HOME}/.antidote)
autoload -Uz $fpath[-1]/antidote
# Generate static file in a subshell when .zsh_plugins.txt is updated.
if [[ ! $zsh_plugins -nt ${zsh_plugins:r}.txt ]]; then
  (antidote bundle <${zsh_plugins:r}.txt >|$zsh_plugins)
fi
# Source your static plugins file.
source $zsh_plugins
unset zsh_plugins
                                                             ### ZSH Plugins }}}

# gotta load asdf early, so language detection works accurately
[[ -f "${HOME}/.asdf/asdf.sh" ]] && . "${HOME}/.asdf/asdf.sh"
fpath=("${HOME}/.asdf/completions" $fpath)

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

autoload -Uz compinit
typeset -i updated_at=$(date +'%j' -r ~/.zcompdump 2>/dev/null || stat -f '%Sm' -t '%j' ~/.zcompdump 2>/dev/null)
if [ $(date +'%j') != $updated_at ]; then
  compinit
else
  compinit -C
fi

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files updated_at

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
# shellcheck disable=SC1090
[ -f ~/.localrc ] && . ~/.localrc

local source_files=(
  "${HOME}/plugins/java/set-java-home.zsh"
  "${XDG_CONFIG_HOME:-${HOME}/.config}/asdf-direnv/zshrc"
  "${HOME}/.fzf.zsh"
  '~/.travis/travis.sh'
  '/opt/google-cloud-sdk/path/path.zsh.inc'
  '/opt/google-cloud-sdk/completion.zsh.inc'
  "${HOME}/.iterm2_shell_integration.zsh"
  "${HOME}/.p10k.zsh"
  "${HOME}/.jfrog/jfrog_zsh_completion"
  "${HOME}/.nix-profile/etc/profile.d/nix.sh"
)
for file in "$source_files[@]"; do
  if [ -f "$file" ]; then
    source "$file"
  fi
done
unset source_files

if if-command kubectl; then
  source <(kubectl completion zsh)
fi
